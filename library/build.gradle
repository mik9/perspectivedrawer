apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "0.3.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.nineoldandroids:library:2.4.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Perspective Drawer'
                packaging 'aar'
                // optionally artifactId can be defined here
                description 'Opensource library that provides new type of navigation drawer for Android applications.'
                url 'https://github.com/mik9/perspectivedrawer'

                groupId = "ua.pl.mik"
                artifactId = "perspectivedrawer"
                version = "0.3.2"

                scm {
                    connection 'scm:git:https://github.com/mik9/perspectivedrawer.git'
                    developerConnection 'scm:git:git@github.com:mik9/perspectivedrawer.git'
                    url 'https://github.com/mik9/perspectivedrawer'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mik_os'
                        name 'Kyrylo Mikos'
                        email 'kiril.mik.os@gmail.com'
                    }
                }
            }
        }
    }
}
dependencies {
    compile 'com.android.support:appcompat-v7:23.2.1'
}